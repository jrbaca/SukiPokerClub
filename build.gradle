plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.61" apply false
    id("org.openjfx.javafxplugin") version "0.0.8" apply false
}

subprojects {
    apply plugin: "org.jetbrains.kotlin.jvm"

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

        // JUnit5 support
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.2.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.2.0'

        // Enable JUnit 5
        test {
            useJUnitPlatform()
        }
    }

    repositories {
        jcenter()
    }

    sourceCompatibility = 11
    targetCompatibility = 11

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

}

project(":launcher") {
    task packageReleaseJar(type: Jar) {
        manifest {
            attributes 'Main-Class': 'MainKt'
        }
        archiveName("Suki's Poker Club.jar")
        from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
        with jar
    }
    packageReleaseJar.dependsOn(build)
}

project(":core") {
    dependencies {
        compile "io.ktor:ktor-websockets:1.3.1"

        // Logging
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
        compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.13.0'
        compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.13.0'
        compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.13.0' // slf4j bridge
        compile 'io.github.microutils:kotlin-logging:1.7.8'
    }
}

project(":client") {
    apply plugin: "application"
    apply plugin: "org.openjfx.javafxplugin"

    mainClassName = 'app.SukiPokerClubClientKt'

    javafx {
        version = "13"
        modules = ['javafx.controls', 'javafx.fxml']
    }

    dependencies {
        implementation project(":core")
        implementation "io.ktor:ktor-client-cio:1.3.1"
    }
}

project(":server") {
    apply plugin: "application"

    mainClassName = 'app.SukiPokerClubServerKt'

    dependencies {
        implementation project(":core")
        implementation "io.ktor:ktor-server-netty:1.3.1"
    }
}